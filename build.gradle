plugins {
    id 'net.researchgate.release' version '3.0.0'
    id 'java'
    id  'maven-publish'
    id 'signing'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}
group = 'fr.maif'

subprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    group = 'fr.maif'

    repositories {
        mavenCentral()
    }

    java {
        withSourcesJar()
        withJavadocJar()
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    ext {
        scalaVersion          = "2.13"
        alpakkaKafkaVersion   = "2.0.7"
        jacksonVersion        = "2.10.5"
        akkaVersion           = "2.6.14"
        vavrVersion           = "0.10.3"
        jooqVersion           = "3.17.4"
        jooqAsyncVersion      = "2.0.0-BETA7"
        functionalJsonVersion = "1.0.3"
        kafkaVersion          = "3.0.1"
        reactorKafkaVersion   = "1.3.12"
        reactorVersion        = "3.4.23"
        vertxSqlVersion       = "4.3.3"
    }

    test {
        testLogging {
            events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components.java)
                pom {
                    name = "Thoth"
                    description = "Reactive event sourcing with java"
                    inceptionYear = "2020"
                    url = "https://github.com/maif/thoth"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = 'alexandre.delegue'
                            name = 'Alexandre Del√®gue'
                            url = 'https://github.com/larousso'
                        }
                        developer {
                            id = 'benjamin.cavy'
                            name = 'Benjamin Cavy'
                            url = 'https://github.com/ptitFicus'
                        }
                    }
                    scm {
                        url = "https://github.com/maif/thoth"
                        connection = "scm:git:git://github.com/maif/thoth.git"
                        developerConnection = "scm:git:ssh://github.com/maif/thoth.git"
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.env.SONATYPE_USERNAME
            password = System.env.SONATYPE_PASSWORD
        }
    }
}

release {
    tagTemplate = 'v${version}'
}
release.git.requireBranch.set('master')

